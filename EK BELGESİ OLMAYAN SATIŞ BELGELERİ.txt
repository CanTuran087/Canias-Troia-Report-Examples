OBJECT: 
 STRING TABLO,
 STRING MAILTEXT,
 STRING SORG,
 STRING TOO,
 STRING CCC,
 STRING SUBJ,
 INTEGER TGUNSAY,
 STRING TBELGETIP;

TGUNSAY = 0;
TBELGETIP = '';
TOO = ''; //iLGİLİ MAIL ADRESLERİNİ BURAYA YAZABİLİRSİNİZ
CCC = ''; //iLGİLİ MAIL ADRESLERİNİ BURAYA YAZABİLİRSİNİZ
SUBJ = 'EK BELGESİ OLMAYAN SATIŞ BELGELERİ';

SELECT '' AS BELGETIP, '' AS BELGENUM, CAST('' AS DATE) AS BELGETARIH,
	 0 AS GUNSAY 
	FROM IASSALHEAD 
	WHERE 1 = 2 
	INTO SIRALITABLO;

SELECT F1.DOCTYPE, F1.DOCNUM, F1.VALIDFROM
	FROM IASSALHEAD F1 
	LEFT OUTER JOIN IASDOCHEAD F2 ON F2.CLIENT = F1.CLIENT 
		AND F2.COMPANY = F1.COMPANY 
		AND F2.SALDOCTYPE = F1.DOCTYPE 
		AND F2.SALDOCNUM = F1.DOCNUM 
	LEFT OUTER JOIN IASSALITEM F3 ON F3.CLIENT = F1.CLIENT 
		AND F3.COMPANY = F1.COMPANY 
		AND F3.REFDOCTYPE = F1.DOCTYPE 
		AND F3.REFDOCNUM = F1.DOCNUM 
		AND F3.ISDELETE = 0 
		AND F3.ISSTOP = 0 
		AND F3.DOCTYPE LIKE 'C%' 
	WHERE F1.CLIENT = SYS_CLIENT
		AND F1.COMPANY = '01' 
		AND F1.ORDSTAT=0 
		AND F1.ISDELETE = 0 
		AND F1.ISSTOP = 0 
		AND F1.DOCTYPE IN ('A1', 'A2', 'A3', 'A4')
		AND F1.VALIDFROM >= '20230101' 
		AND F2.CLIENT IS NULL 
		AND F3.CLIENT IS NULL 
	INTO TMPTABLE;

TABLO = '<!DOCTYPE html> <html> <body>';

IF TMPTABLE_ROWCOUNT > 0 THEN
	TABLO = TABLO + '<h3>A1, A2, A3 VE A4 BELGE TİPLERİ TABLODAKİ GİBİDİR,</h3>';
	TABLO = TABLO + '<table border = "1" style="width:340%;">';
	TABLO = TABLO + '<tr style="font-size:12px;"><th>BELGE TİPİ</th><th>BELGE NUMARASI</th><th>SİPARİŞ BELGE TARİHİ</th><th>CLİ BELGEDEN İTİBAREN KAÇ GÜN GEÇTİ</th></tr>';

	LOOP AT TMPTABLE 
	BEGIN
		SELECT F3.DOCTYPE, F3.VALIDFROM 
			FROM IASSALITEM F1 
			INNER JOIN IASSALHEAD F2 ON F2.CLIENT = F1.CLIENT 
				AND F2.COMPANY = F1.COMPANY 
				AND F2.DOCTYPE = F1.DOCTYPE 
				AND F2.DOCNUM = F1.DOCNUM 
				AND F2.ISDELETE = 0 
				AND F2.ISSTOP = 0 
			INNER JOIN IASSALITEM F3 ON F3.CLIENT = F2.CLIENT 
				AND F3.COMPANY = F2.COMPANY 
				AND F3.REFDOCTYPE = F2.DOCTYPE 
				AND F3.REFDOCNUM = F2.DOCNUM 
				AND F3.ISDELETE = 0 
				AND F3.ISSTOP = 0 
			INNER JOIN IASSALHEAD F4 ON F4.CLIENT = F3.CLIENT 
				AND F4.COMPANY = F3.COMPANY 
				AND F4.DOCTYPE = F3.DOCTYPE 
				AND F4.DOCNUM = F3.DOCNUM 
				AND F4.ISDELETE = 0 
				AND F4.ISSTOP = 0 
			WHERE F1.CLIENT = SYS_CLIENT
				AND F1.COMPANY = '01' 
				AND F1.REFDOCTYPE = TMPTABLE_DOCTYPE 
				AND F1.REFDOCNUM = TMPTABLE_DOCNUM 
				AND F1.ISDELETE = 0 
				AND F1.ISSTOP = 0 
				AND F3.DOCTYPE LIKE 'C%' 
			INTO C3C5VY;

		APPEND ROW TO SIRALITABLO;

		IF SELECTED THEN
			TGUNSAY = GETDAYDIFF(C3C5VY_VALIDFROM,SYS_CURRENTDATE);
			TBELGETIP = TMPTABLE_DOCNUM + ' ('+C3C5VY_DOCTYPE+') VAR';
		ELSE
		    TBELGETIP = TMPTABLE_DOCNUM;
		ENDIF;

		MOVE TMPTABLE_DOCTYPE TO SIRALITABLO_BELGETIP;
		MOVE TBELGETIP TO SIRALITABLO_BELGENUM;
		MOVE TMPTABLE_VALIDFROM TO SIRALITABLO_BELGETARIH;
		MOVE TGUNSAY TO SIRALITABLO_GUNSAY;
	ENDLOOP;

	SORT SIRALITABLO ON DESC GUNSAY;

	LOOP AT SIRALITABLO 
	BEGIN
		TABLO = TABLO + '<tr><td>#DOCTYPE#</td><td>#DOCNUM#</td><td>#VALIDFROM#</td><td>#GUNSAY#</td></tr>';
		TABLO = REPLACE(TABLO,'#DOCTYPE#','SIRALITABLO_BELGETIP');
		TABLO = REPLACE(TABLO,'#DOCNUM#','SIRALITABLO_BELGENUM');
		TABLO = REPLACE(TABLO,'#VALIDFROM#','SIRALITABLO_BELGETARIH');
		TABLO = REPLACE(TABLO,'#GUNSAY#','SIRALITABLO_GUNSAY');
	ENDLOOP;

ENDIF;

SELECT  F1.DOCTYPE, F1.DOCNUM, F1.VALIDFROM, F1.EINVONUMBER AS EFATNO, F1.LTEXT 
	FROM IASSALHEAD F1 
	LEFT OUTER JOIN IASDOCHEAD F2 ON F2.CLIENT = F1.CLIENT 
		AND F2.COMPANY = F1.COMPANY 
		AND F2.SALDOCTYPE = F1.DOCTYPE 
		AND F2.SALDOCNUM = F1.DOCNUM 
	WHERE F1.CLIENT = SYS_CLIENT
		AND F1.COMPANY = '01' 
		AND F1.ORDSTAT=0 
		AND F1.ISDELETE = 0 
		AND F1.ISSTOP = 0 
		AND F1.DOCTYPE IN ('B1','B2', 'B3') 
		AND F1.VALIDFROM >= '20230101' 
		AND F2.DOCID IS NULL 
	INTO TMPTABLE2;


IF TMPTABLE2_ROWCOUNT > 0 THEN
	TABLO = TABLO + '</table><table><br><h3>B1, B2 VE B3 BELGE TİPLERİ TABLODAKİ GİBİDİR,</h3>';
	TABLO = TABLO + '<table border = "1" style="width:340%;">';
	TABLO = TABLO + '<tr style="font-size:12px;"><th>BELGE TİPİ</th><th>BELGE NUMARASI</th><th>BELGE TARİHİ</th><th>E-FATURA NO</th><th>NOTLAR</th></tr>';

	LOOP AT TMPTABLE2 
	BEGIN
		TABLO = TABLO +  '<tr><td>#DOCTYPE#</td><td>#DOCNUM#</td><td>#VALIDFROM#</td><td>#EFATNO#</td><td>#LTEXT#</td></tr>';
		TABLO = REPLACE(TABLO,'#DOCTYPE#','TMPTABLE2_DOCTYPE');
		TABLO = REPLACE(TABLO,'#DOCNUM#','TMPTABLE2_DOCNUM');
		TABLO = REPLACE(TABLO,'#VALIDFROM#','TMPTABLE2_VALIDFROM');
		TABLO = REPLACE(TABLO,'#EFATNO#','TMPTABLE2_EFATNO');
		TABLO = REPLACE(TABLO,'#LTEXT#','TMPTABLE2_LTEXT');
	ENDLOOP;

ENDIF;

TABLO = TABLO + '</table></body></html>';
MAILTEXT = TABLO;

IF TMPTABLE_ROWCOUNT > 0 
		|| TMPTABLE2_ROWCOUNT > 0 THEN
	SENDMAIL MESSAGE MAILTEXT TOADDRESS TOO CCADDRESS CCC TYPE 'HTML' SUBJECT SUBJ;

	IF ! SYS_STATUS THEN
		MESSAGE EFE I0 WITH 'MAİL GÖNDERİLDİ';
	ELSE
		MESSAGE EFE E0 WITH 'MAİL GÖNDERİLEMEDİ';
	ENDIF;

ENDIF;
