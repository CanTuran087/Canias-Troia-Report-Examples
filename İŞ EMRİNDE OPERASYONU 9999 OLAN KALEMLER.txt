OBJECT: 
 STRING TABLO,
 STRING MAILTEXT,
 STRING SORG,
 STRING TOO,
 STRING CCC,
 STRING SUBJ,
 USERS USERSREC;

TOO = ''; //İLGİLİ MAIL ADRESLERİNİ GİRİNİZ
CCC = ''; //İLGİLİ MAIL ADRESLERİNİ GİRİNİZ

CCC = CCC + ',' + USERSREC.GETUSERMAILBYGRPINFO(MAILGROUP);
SUBJ = 'İŞ EMRİNDE OPERASYONU 9999 OLAN KALEMLER';

SELECT F1.CUSTORDERNUM, F1.PRDORDER, F1.MATERIAL, F3.STEXT,
	 F2.MATERIAL AS MATERIAL2, F4.STEXT STEXT2, F2.OPERATION 
	FROM IASPRDORDER F1 
	INNER JOIN IASMATRES F2 ON F2.CLIENT = F1.CLIENT 
		AND F2.COMPANY = F1.COMPANY 
		AND F2.POTYPE = F1.POTYPE 
		AND F2.PRDORDER = F1.PRDORDER 
		AND F2.ISDELETE = 0 
		AND F2.OPERATION = 9999 
	INNER JOIN IASMATX F3 ON F3.CLIENT = F1.CLIENT 
		AND F3.COMPANY = F1.COMPANY 
		AND F3.MATERIAL = F1.MATERIAL 
		AND F3.PLANT = '*' 
		AND F3.LANGU = 'T' 
	INNER JOIN IASMATX F4 ON F4.CLIENT = F2.CLIENT 
		AND F4.COMPANY = F2.COMPANY 
		AND F4.MATERIAL = F2.MATERIAL 
		AND F4.PLANT = '*' 
		AND F4.LANGU = 'T' 
	WHERE F1.CLIENT = SYS_CLIENT
		AND F1.COMPANY = '01' 
		AND F1.STATUS3 = 0 
		AND F1.STATUS4 = 0 
		AND F1.ISDELETE = 0 
	ORDERBY F1.CUSTORDERNUM, F1.PRDORDER 
	INTO TMPOPRTABLE;


IF TMPOPRTABLE_ROWCOUNT > 0 THEN
	TABLO = '<!DOCTYPE html> <html> <body><h3>İŞ EMRİNDE OPERASYONU 9999 OLAN KALEMLER TABLODAKİ GİBİDİR,</h3> <br>';
	TABLO = TABLO + '<table border = 1>';
	TABLO = TABLO + '<tr><th>SİPARİŞ NUMARASI</th><th>İŞ EMRİ NUMARASI</th><th>MALZEME KODU</th><th>MALZEME AÇIKLAMASI</th>';
	TABLO = TABLO + '<th>KALEM KODU</th><th>KALEM AÇIKLAMASI</th><th>OPERASYON KODU</th></tr>';

	LOOP AT TMPOPRTABLE 
	BEGIN
		TABLO = TABLO +  '<tr><td>' + TMPOPRTABLE_CUSTORDERNUM + '</td><td>' + TMPOPRTABLE_PRDORDER + '</td><td>' + TMPOPRTABLE_MATERIAL;
		TABLO = TABLO +  '</td><td>' + TMPOPRTABLE_STEXT + '</td><td>' + TMPOPRTABLE_MATERIAL2 + '</td><td>' + TMPOPRTABLE_STEXT2;
		TABLO = TABLO +  '</td><td style="background-color:red; color:white;">' + TMPOPRTABLE_OPERATION + '</td></tr>';
	ENDLOOP;

	TABLO = TABLO + '</table></body></html>';
	MAILTEXT = TABLO;
	SENDMAIL MESSAGE MAILTEXT TOADDRESS TOO CCADDRESS CCC TYPE 'HTML' SUBJECT SUBJ;

	IF ! SYS_STATUS THEN
		MESSAGE EFE I0 WITH 'MAİL GÖNDERİLDİ';
	ELSE
		MESSAGE EFE E0 WITH 'MAİL GÖNDERİLEMEDİ';
	ENDIF;

ENDIF;